apply plugin: 'com.github.johnrengelman.shadow'

description = 'Okapi Shade contains the full Okapi stack and its relocated dependencies'

dependencies {
    compile project(':okapi-relational')
}

shadowJar {
    classifier = ''
    def relocatePackage = 'org.opencypher.relocated'
    [
            'cats',
            'fastparse',
            'machinist',
            'org.apache',
            'org.atnos',
            'org.parboiled',
            'sourcecode',
            'ujson',
            'upack',
            'upickle',
    ].each { pkg ->
        relocate(pkg, "${relocatePackage}.$pkg")
    }

    dependencies {
        exclude(dependency('org.scala-lang:'))
        exclude(dependency('org.scala-lang.modules:'))
    }
    exclude "META-INF/versions/**/*"
    exclude "META-INF/services/**/*"
    exclude "utf8.json"
}

pub.dev.artifacts = ['shadowJar']
pub.full.artifacts = ['shadowJar']

assemble.dependsOn shadowJar
