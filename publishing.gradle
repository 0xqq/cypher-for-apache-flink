subprojects {
    apply plugin: 'maven-publish'

    afterEvaluate {

        publishing {

            repositories {
                maven {
                    name = 'buildDir'
                    url = "file://$rootProject.buildDir/repo"
                }
            }

            publications {
                full(MavenPublication) {
                    from components.java

                    ['sourceJar', 'testJar', 'shadowJar']
                            .collect { tasks.findByName(it) }
                            .each { if (it != null) artifact it }

                    pom {
                        licenses {
                            license {
                                name = 'Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }
                        developers {
                            developer {
                                id = 'caps'
                                name = 'The CAPS team'
                                email = 'opencypher@neo4j.com'
                                url = 'https://www.opencypher.org'
                            }
                        }
                        scm {
                            url = 'https://github.com/opencypher/cypher-for-apache-spark'
                        }
                    }
                }
            }

        }
    }

}